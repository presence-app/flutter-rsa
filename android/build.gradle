group 'dev.jerson.fat_rsa'
version '1.0'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.5.1'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

android {
    // Conditional for compatibility with AGP <4.2.
    if (project.android.hasProperty("namespace")) {
        namespace 'dev.jerson.fast_rsa'
    }
    
    // Specify NDK version that supports 16KB page size alignment
    ndkVersion "29.0.14206865"
    
    compileSdkVersion 36

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    defaultConfig {
        minSdkVersion 26
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        
        // Support for 16KB page size
        ndk {
            // Ensure all ABIs are built with 16KB page size support
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64'
        }
    }
    
    lintOptions {
        disable 'InvalidPackage'
    }
    
    packagingOptions {
        // Ensure native libraries support 16KB page size
        jniLibs {
            useLegacyPackaging = false
        }
    }
}

task nativeLibsToJar(type: Jar, description: 'create a jar archive of the native libs') {
    destinationDirectory.set(file("$buildDir/native-libs"))
    archiveBaseName = 'native-libs'
    from fileTree(dir: 'libs', include: '**/*.so')
    into 'lib/'
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn(nativeLibsToJar)
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

dependencies {
    api fileTree(dir: "$buildDir/native-libs", include: 'native-libs.jar')
}
